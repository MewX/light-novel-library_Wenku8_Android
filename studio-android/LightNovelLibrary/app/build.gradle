apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'

android {
    compileSdkVersion rootProject.targetSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "org.mewx.wenku8"
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 49
        versionName "1.16"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            testProguardFile 'proguard-rules-tests.pro' // FIXME - the rule does not work
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                // Default for arm only.
                abiFilters 'armeabi-v7a' , 'arm64-v8a' //, 'x86' , 'x86_64'
            }
        }
    }
    flavorDimensions "default"
    productFlavors {
        alpha {
            // 内测渠道，群分发
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "alpha"]
        }

        baidu {
            // 百度/其他网盘分发
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "baidu"]
        }

        playstore {
            // Google Play Store
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "playstore"]
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a' //, 'x86' , 'x86_64'
            }
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', '.readme']
        }
    }

    aaptOptions {
        cruncherEnabled = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'org.mewx.wenku8'
}

dependencies {
    // external libraries
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.getbase:floatingactionbutton:1.9.0'
    implementation 'com.makeramen:roundedimageview:2.0.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.1'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.jpardogo.googleprogressbar:library:1.2.0'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'com.nononsenseapps:filepicker:2.2'
    implementation 'com.umeng.umsdk:common:9.5.6'
    implementation 'com.umeng.umsdk:asms:1.6.3'
    implementation 'com.umeng.umsdk:crash:0.0.5'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.19.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.7.4'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/alpha/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}
