apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'

android {
    compileSdkVersion rootProject.targetSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "org.mewx.wenku8"
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 36
        versionName "1.5"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // this one has to be added due to the intermediate outputs for debug
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            testProguardFile 'proguard-rules-tests.pro' // FIXME - the rule does not work
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        alpha {
            // 内测渠道，群分发
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "alpha"]
        }

        baidu {
            // 百度网盘分发
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "baidu"]
        }

        coolapk {
            // 酷安
            dimension "default"
            manifestPlaceholders = [ UMENG_CHANNEL_NAME : "coolapk"]
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    aaptOptions {
        cruncherEnabled = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:support-v4:' + rootProject.supportLibVersion
    implementation 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:design:' + rootProject.supportLibVersion
    // external libraries
    implementation project(':volley') // DO NOT CHANGE THIS, MODIFIED VERSION
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.getbase:floatingactionbutton:1.9.0'
    implementation 'com.makeramen:roundedimageview:2.0.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.1'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.jpardogo.googleprogressbar:library:1.2.0'
    implementation 'org.adw.library:discrete-seekbar:1.0.0'
    implementation 'com.nononsenseapps:filepicker:2.2'
    implementation 'com.umeng.sdk:common:1.5.0'
    implementation 'com.umeng.sdk:analytics:7.5.0'

    androidTestImplementation 'com.android.support:support-annotations:' + rootProject.supportLibVersion
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'junit:junit:4.12'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.7.4'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/alpha/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}
